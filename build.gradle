buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'//添加Maven发布插件
configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

group 'so.sao'
version '1.0.34-RELEASE'
description = """shop-dada"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url "https://nexus.sao.so/repository/maven-public/"
        credentials {
            username nexusUserName
            password nexusPwd
        }
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'

    optional "org.springframework.boot:spring-boot-configuration-processor"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJava.dependsOn(processResources)

// 打包
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
// 如果是发行版本，则上传到发行库。否则上传到快照库。
def postfixUrl = "repository/maven-snapshots"
if (project.version.endsWith("-RELEASE")) {
    postfixUrl = "repository/maven-releases"
}
// 发布到本地Maven库和远程Maven服务器
publishing {
    repositories {
        maven {
            credentials {
                username nexusUserName
                password nexusPwd
            }
            url "https://nexus.sao.so/$postfixUrl/"
        }
    }
}